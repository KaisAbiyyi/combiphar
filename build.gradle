plugins {
    id 'java'
    id 'application'
    id 'eclipse'
}

group = 'io.combiphar-used-goods'
version = '1.0-SNAPSHOT'

repositories { mavenCentral() }

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    implementation 'io.javalin:javalin:6.7.0'
    implementation 'io.javalin:javalin-rendering:6.7.0'

    // Pebble "old coordinates" yang masih punya package com.mitchellbosecke.pebble.*
    implementation 'com.mitchellbosecke:pebble:2.4.0'

    // Jackson for JSON serialization
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.2'

    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'com.mysql:mysql-connector-j:8.3.0'
    implementation 'org.mindrot:jbcrypt:0.4'

    runtimeOnly 'org.slf4j:slf4j-simple:2.0.16'
}



application {
    mainClass = 'Main'
}

// Helper to load .env file
def loadDotEnv() {
    def env = [:]
    def envFile = file('.env')
    if (envFile.exists()) {
        envFile.eachLine { line ->
            if (line.trim() && !line.startsWith('#')) {
                def parts = line.split('=', 2)
                if (parts.length == 2) {
                    def key = parts[0].trim()
                    def value = parts[1].trim().replaceAll(/^"|"$/, '')
                    env[key] = value
                }
            }
        }
    }
    return env
}

tasks.withType(JavaExec) {
    environment loadDotEnv()
}

// Task to run admin seeder
task seed(type: JavaExec) {
    group = 'database'
    description = 'Create admin account'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.combiphar.core.seeder.AdminSeeder'
}

// Task to run category seeder
task seedCategories(type: JavaExec) {
    group = 'database'
    description = 'Seed sample categories'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.combiphar.core.seeder.CategorySeeder'
}

// Task to run product seeder
task seedProducts(type: JavaExec) {
    group = 'database'
    description = 'Seed sample products with dummy data'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.combiphar.core.seeder.ProductSeeder'
}

// Task to run QC items seeder
task seedQCItems(type: JavaExec) {
    group = 'database'
    description = 'Seed sample items with NEEDS_QC status for QC Pipeline'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.combiphar.core.seeder.QCItemSeeder'
}

// Demo task to validate cart persistence
task demoCart(type: JavaExec) {
    group = 'database'
    description = 'Run a small demo that saves and loads a cart for a demo user'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.combiphar.core.seeder.CartDemoSeeder'
}
